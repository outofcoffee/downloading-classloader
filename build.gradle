group 'com.gatehill.dlcl'
version '0.1.9-SNAPSHOT'

buildscript {
    ext.mavenSnapshotRepository = 's3://gatehillsoftware-maven/snapshots'
    ext.awsAccessKey = System.getenv('AWS_ACCESS_KEY_ID') ?: project.findProperty('AWS_ACCESS_KEY_ID')
    ext.awsSecretKey = System.getenv('AWS_SECRET_ACCESS_KEY') ?: project.findProperty('AWS_SECRET_ACCESS_KEY')

    ext.kotlin_version = '1.1.2-5'
    ext.resolverVersion = '1.0.3'
    ext.mavenVersion = '3.5.0'
    ext.version_log4j = '2.5'
    ext.version_junit_platform_gradle = '1.0.0-M4'
    ext.version_spek = '1.1.2'
    ext.version_kluent = '1.22'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$version_junit_platform_gradle"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://dl.bintray.com/jetbrains/spek' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile "org.apache.maven.resolver:maven-resolver-api:${resolverVersion}"
    compile "org.apache.maven.resolver:maven-resolver-spi:${resolverVersion}"
    compile "org.apache.maven.resolver:maven-resolver-util:${resolverVersion}"
    compile "org.apache.maven.resolver:maven-resolver-impl:${resolverVersion}"
    compile "org.apache.maven.resolver:maven-resolver-connector-basic:${resolverVersion}"
    compile "org.apache.maven.resolver:maven-resolver-transport-file:${resolverVersion}"
    compile "org.apache.maven.resolver:maven-resolver-transport-http:${resolverVersion}"
    compile "org.apache.maven:maven-resolver-provider:${mavenVersion}"

    // redirect all logs to log4j2
    compile "org.apache.logging.log4j:log4j-core:$version_log4j"
    compile "org.apache.logging.log4j:log4j-jcl:$version_log4j"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$version_log4j"
    compile "org.apache.logging.log4j:log4j-1.2-api:$version_log4j"
    compile "org.apache.logging.log4j:log4j-jul:$version_log4j"

    // specifications
    testCompile "org.amshove.kluent:kluent:$version_kluent"
    testCompile "org.jetbrains.spek:spek-api:$version_spek"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$version_spek"

    // fix for https://github.com/JetBrains/spek/issues/195
    testCompile "org.junit.platform:junit-platform-launcher:1.0.0-M4"
}

junitPlatform {
    platformVersion version_junit_platform_gradle
    filters {
        engines {
            include 'spek'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            repositories {
                maven {
                    url mavenSnapshotRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}
